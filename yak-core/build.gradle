plugins {
    id 'java-library'
    id 'jacoco'
    id 'checkstyle'
}

java {
    withSourcesJar()
}

repositories {
    mavenCentral()
}

group = "org.guardiandev"
version = "1.0.0-SNAPSHOT"
sourceCompatibility = '11'

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

tasks.named('test') {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        html.enabled true
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit {
                counter = 'INSTRUCTION'
                minimum = 0.80
            }
        }
    }
}

checkstyle {
    toolVersion = "8.41"
    configDirectory = file("${rootProject.rootDir}/config/checkstyle")
    maxErrors = 0
    maxWarnings = 0
}

check.dependsOn jacocoTestReport
check.finalizedBy javadoc, jacocoTestCoverageVerification