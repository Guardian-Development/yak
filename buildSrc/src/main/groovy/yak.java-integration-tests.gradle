sourceSets {
    // adds integration test source set
    intTest {
        java.srcDir "$projectDir/src/intTest/java"
        resources.srcDir "$projectDir/src/intTest/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    intTestImplementation.extendsFrom testImplementation
    intTestRuntime.extendsFrom testRuntime
}

// run the intTest/** tests
task intTest(type: Test) {
    description = 'runs int tests'
    group = 'verification'
    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
}

intTest {
    mustRunAfter test
    useJUnitPlatform()
    testLogging {
        events "started", "failed", "passed", "standard_out"
    }

    jvmArgs('--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED')
    jvmArgs('--add-opens', 'java.base/java.util.zip=ALL-UNNAMED')
    jvmArgs('--add-opens', 'java.base/jdk.internal.ref=ALL-UNNAMED')
    jvmArgs('--add-opens', 'java.base/java.lang=ALL-UNNAMED')
    jvmArgs('--add-opens', 'java.management/sun.management=ALL-UNNAMED')
    jvmArgs('--add-opens', 'jdk.management/com.sun.management.internal=ALL-UNNAMED')
    jvmArgs('--add-modules', 'java.se')
}

check.dependsOn intTest
